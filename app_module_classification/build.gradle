if (isBuildAsModule.toBoolean()) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.ext.android.compileSdk

    // 默认配置
    defaultConfig {
        if (!isBuildAsModule.toBoolean()) {
            applicationId rootProject.ext.android.applicationId
        }
        minSdkVersion rootProject.ext.android.minSdk
        targetSdkVersion rootProject.ext.android.targetSdk
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // ARouter 路由
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    sourceSets{
        main {
            if (isBuildAsModule.toBoolean()) {
                manifest.srcFile 'src/main/library/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // support
    implementation rootProject.ext.dependencies["androidSupportV7"]
    implementation rootProject.ext.dependencies["recyclerviewV7"]
    implementation rootProject.ext.dependencies["androidSupportConstraint"]

    // test
    testImplementation rootProject.ext.dependencies["junitSupport"]
    androidTestImplementation rootProject.ext.dependencies["supportTestRunner"]
    androidTestImplementation rootProject.ext.dependencies["supportTestEspresso"]

    // dagger annotationProcessor
    kapt rootProject.ext.dependencies["daggerAndroidProcessor"]
    kapt rootProject.ext.dependencies["daggerCompilerProcessor"]
    // ButterKnife annotationProcessor
    kapt rootProject.ext.dependencies["butterknifeCompiler"]
    // ARouter 路由 https://github.com/alibaba/ARouter
    kapt rootProject.ext.dependencies["arouterCompiler"]


    implementation project(":app_lib_common")
}
