apply plugin: 'com.android.library'
apply plugin: 'org.greenrobot.greendao' // apply plugin 放在最后的话编译时报错
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.ext.android.compileSdk

    // 默认配置
    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdk
        targetSdkVersion rootProject.ext.android.targetSdk
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // ARouter 路由
        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
        resolutionStrategy.force "com.android.support:support-v4:${rootProject.ext.dependVersion.androidSupportV7}"
    }

}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    
    // rxJavaPermissions https://github.com/tbruyelle/RxPermissions
    api rootProject.ext.dependencies["rxpermissions"]
    // banner https://github.com/youth5201314/banner
    api rootProject.ext.dependencies["banner"]
    // ViewPager Indicator https://github.com/itrenjunhua/MagicIndicator
    api rootProject.ext.dependencies["magicIndicator"]
    // greenDao https://github.com/greenrobot/greenDAO
    api rootProject.ext.dependencies["greendao"]
    // kotlin 支持
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // dagger annotationProcessor
    kapt rootProject.ext.dependencies["daggerAndroidProcessor"]
    kapt rootProject.ext.dependencies["daggerCompilerProcessor"]
    // ARouter 路由 https://github.com/alibaba/ARouter
    kapt rootProject.ext.dependencies["arouterCompiler"]

    // MVPBase
    api project(':mvp_mvpbase')
    // daggerSupport（包含了 rxJavaSupport）
    api project(':mvp_daggersupport')
    // 路由库
    api project(':lib_arouter')
    // http 库
    api project(':lib_http')
    // 常用工具库
    api project(':lib_android_utils')
    api project(':lib_android_view')
    // 导入图片加载库
    api project(':lib_imageloader_interface')
    api project(':lib_imageloader_glide')
    // 自定义缓存库
    api project(':lib_android_cache')
}
